buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
    }
    repositories {
        jcenter()
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo1.maven.org/maven2" }
//       maven { url "http://localhost:8081/content/groups/all-gradle/" }
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

def dropwizard_version = '0.9.3'
def slf4j_version = '1.7.13'
def bintray_org = 'cfpilot'

def artifact_version = '0.1.0'
group = 'nl.rabobank.gict.cf.demo'
version = artifact_version

// we create a file used by the next Jenkins job (so it knows where to find the artifact in bintray)
def propFile = new File('envvars.properties')
propFile.write('GROUPID='+group+'\nARTIFACTID='+project.name+'\nVERSION='+version+'\n')

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'spring-boot'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
description "demonstration app, built with DropWizard, built for cloud foundry"

repositories {
     jcenter()
     mavenCentral()
//    maven { url "http://localhost:8081/content/groups/all-gradle/" }
}

dependencies {
    compile 'org.slf4j:slf4j-api:'+slf4j_version,
            'io.dropwizard:dropwizard-core:'+dropwizard_version,
            'io.dropwizard:dropwizard-assets:'+dropwizard_version,
            'dnsjava:dnsjava:2.1.7',
            'com.readytalk:metrics3-statsd:4.1.0'

    testCompile 'junit:junit:3.8.2',
            'io.dropwizard:dropwizard-testing:'+dropwizard_version
}

springBoot {
    executable = false
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintray_org
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/metskem/'+project.name+'.git'
        version {
            desc = 'this is version specific information'
            attributes = ['test-attribute': 'test-attribute.value']
        }
    }
}
